<html><head></head>
<body>
<pre>
Problema. Se da un sir de valori numerice intregi reprezentate pe dublucuvinte. 
Sa se determine suma cifrelor numarului multiplilor de 8 din sirul octetilor 
inferiori ai cuvintelor superioare din elementele sirului de dublucuvinte.

Exemplu: Daca avem sirul de dublucuvinte
	sir   dd   12300011h, 0abcd0h, 2218acach, 7a082ae3h, 0c820ddd1h, 7828ddd1h, 0c8d8ddd1h, 0c8b0ddd1h, 7260ddd1h, 2a70ddd1h, 7850ddd1h, 0c840ddd1h

Acest sir va fi reprezentat in memorie in format little-endian astfel (de la adrese mici spre adrese mari):
11h| 00h| 30h| 12h| d0h| bch| 0ah| 00h| ach| ach| 18h| 22h| e3h| 2ah| 08h| 7ah| d1h| ddh| 20h| c8h| d1h| ddh| 28h| 78h| d1h| ddh| d8h| c8h| d1h| ddh| b0h| c8h| d1h| ddh| 60h| 72h| d1h| ddh| 70h| 2ah| d1h| ddh| 50h| 78h| d1h| ddh| 40h| c8h 

Cuvintele superioare din aceste dublucuvinte vor fi:
30h| 12h| 0ah| 00h| 18h| 22h| 08h| 7ah| 20h| c8h| 28h| 78h| d8h| c8h| b0h| c8h| 60h| 72h| 70h| 2ah| 50h| 78h| 40h| c8h 

Octetii inferiori ai cuvintelor superioare din elementele sirului de dublucuvinte vor fi:
30h| 0ah| 18h| 08h| 20h| 28h| d8h| b0h| 60h| 70h| 50h| 40h

Numarul multiplilor de 8 din sirul octetilor inferiori ai cuvintelor superioare din elementele 
sirului de dublucuvinte este 11, iar suma cifrelor acestui numar este 2. 

Solutie: Parcurgand sirul de dublucuvinte vom obtine intai numarul multiplilor de 8 din 
sirul octetilor inferiori ai cuvintelor superioare din elementele sirului. Apoi vom obtine 
cifrele acestui numar prin impartiri succesive la 10 si vom calcula suma lor.


assume ds: data, cs: code
data segment
	sir  dd  12300011h,0abcd0h,2218acach,7a082ae3h,0c820ddd1h,7828ddd1h,0c8d8ddd1h,0c8b0ddd1h,7260ddd1h,2a70ddd1h,7850ddd1h,0c840ddd1h
	len equ ($-sir)/4	;lungimea sirului (in dublucuvinte)
	opt db 8		;variabila folosita pentru testarea divizibilitatii cu 8
	zece db 10		;variabila folosita pentru determinarea cifrelor unui numar prin impartiri succesive la 10
	suma db  0		;variabila in care retinem suma cifrelor
data ends
code segment
start:
	mov ax, data
	mov ds, ax
 
	mov si, offset sir	;in ds:si punem adresa (far a) sirului sir 
	cld			;parcurgem sirul de la stanga la dreapta (DF=0).    
	mov cx, len		;vom parcurge elementele sirului intr-o bucla loop cu len iteratii.
	mov bx, 0		;in registrul bx vom retine numarul multiplilor lui 8.
	repeta:
		lodsw		;in ax vom avea cuvantul mai putin semnificativ al dublucuvantului curent din sir
		lodsw		;in ax vom avea cuvantul cel mai semnificativ al dublucuvantului curent din sir
		mov ah, 0	;ne intereseaza doar octetul mai putin semnificativ din acest cuvant (AL)
		div opt		;vedem daca al este divizibil cu 8
		cmp ah, 0	;daca restul nu este 0, reluam ciclul repeta. 
				;Altfel incrementam numarul multiplilor de 8 din registrul bx. 
		jnz nonmultiplu
		inc bx

		nonmultiplu:
			loop repeta		;daca mai sunt elemente de parcurs(cx>0) reia ciclul.

	;mai departe, obtinem cifrele numarului bx in baza 10 prin impartiri succesive la 10 si calculam suma acestor cifre. 

	mov ax, bx
	transf:
		div zece	;impartim la 10 numarul din registrul ax ca sa aflam ultima cifra; cifra va fi in AH
	add suma, ah		;adunam cifra la suma.
	cmp al, 0
	jz sfarsit		;daca catul (din al) este 0 inseamna ca am obtinut toate cifrele si putem parasi bucla transf
				;Altfel, il pregatim pentru o noua iteratie 
	mov ah, 0				
	jmp transf		;reluam bucla pentru obtinerea unei noi cifre.

sfarsit:			;incheiem programul.
	mov ax, 4C00h
	int 21h	
code ends
end start

</pre>
</body>
</html>



